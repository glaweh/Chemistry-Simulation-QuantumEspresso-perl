#!/usr/bin/perl
use strict;
use Getopt::Std;

my $column_seperator="\n";
# -----------------------------------------------------------------
#                        Extractors
# Extract energy
sub extract_E {
	my $fh = shift;
	# --- matching line: ---
	# !    total energy              =  -150.90270828 ryd
	my @E = map { my @l=split; $l[4] } grep (/^!/,<$fh>);
	return @E;
}
# Extract forces
sub extract_F {
	my $fh = shift;
	my @F;
	my @block=();
	my $trigger=0;
	# --- matching block: ---
	#
	#      atom   1 type  1   force =     0.01955501    0.02827548    0.00185099
	#      atom   2 type  2   force =     0.00248174   -0.02624067    0.10253621
	#      atom   3 type  2   force =    -0.02203676   -0.00203481   -0.10438720
	#
	while (<$fh>) {
		if (m#atom\s+\d+ type\s+\d+\s+force =(.*)$#) {
			$trigger=1;
			$_=$1;
			push @block,split;
		} elsif ($trigger) {
			$trigger=0;
			push @F,join($column_seperator,@block);
			@block=();
		}
	}
	return @F;
}
# Extract stress
sub extract_S {
	my $fh=shift;
	my @S;
	my $trigger=0;
	my @block=();
	# --- matching block: ---
	#
	#          total   stress  (ryd/bohr**3)                  (kbar)     P=  -66.79
	#  -0.00041355   0.00000101  -0.00006949        -60.84      0.15    -10.22
	#   0.00000101  -0.00038236   0.00006165          0.15    -56.25      9.07
	#  -0.00006949   0.00006165  -0.00056612        -10.22      9.07    -83.28
	#
	while (<$fh>) {
		if ($trigger) {
			if (m#^\s*$#){
				$trigger=0;
				push @S,join($column_seperator,@block);
				@block=();
			} else {
				push @block,split;
				$#block-=3;
			}
		}
		$trigger ||= m#total   stress#;
	};
	return @S;
}
# extract Fermi energy
sub extract_Ef {
	my $fh=shift;
	my @Ef;
	while (<$fh>) {
		m#Fermi energy is\s+(\S+) ev$# and push @Ef,$1;
	}
	return @Ef;
}
# extract phonon frequencies
sub extract_omega {
	my $fh=shift;
	my @omega;
	# match:   omega( 1) =      -2.614365 [THz] =     -87.206422 [cm-1]
	while (<$fh>) {
		m#^\s+omega.*=\s*([0-9.-]+) \[cm-1\]# and push @omega,$1;
	}
	return join($column_seperator,@omega);
}
# extract occupation matrix traces
sub extract_trn {
	my $fh=shift;
	my %occ;
	my @n;
	# match:atom  1   Tr[ns(na)]=  47.0181269
	while (<$fh>) {
		m#^atom\s+(\d+)\s+Tr\[ns\(na\)\]=\s*([0-9.-]+)# and push(@{$occ{$1}},$2);
	}
	# bare
	foreach my $atom (sort { $a <=> $b }  keys %occ) {
		push @n, ${$occ{$atom}}[1];
	}
	# screened
	foreach my $atom (sort { $a <=> $b }  keys %occ) {
		push @n, pop(@{$occ{$atom}});
	}
	return join($column_seperator,@n);
}

# extract absolute and total magnetization
sub extract_mabs {
	my $fh=shift;
	my @m_abs;

	while (<$fh>) {
		#match: absolute magnetization    =     0.05 Bohr mag/cell
		m#absolute magnetization\s*=\s*([0-9.-]+)\s*Bohr# and push @m_abs,$1;
	}
	return @m_abs;
}

sub extract_mtot {
	my $fh=shift;
	my @m_total;

	while (<$fh>) {
		#match: total magnetization       =     0.00 Bohr mag/cell
		m#total magnetization\s*=\s*([0-9.-]+)\s*Bohr# and push @m_total,$1;
	}
	return @m_total;
}
sub extract_ctime {
	my $fh=shift;
	my $ctime;
	while (<$fh>) {
		#match:     PWSCF        :    11h32m CPU time,       12h 9m wall time
		if (m#:.*,\s*(.*) wall time#) {
			my $thistime=0;
			$_=$1;
			m#([0-9.]+)d# and $thistime+=$1*24;
			m#([0-9.]+)h# and $thistime+=$1;
			m#([0-9.]+)m# and $thistime+=$1/60;
			m#([0-9.]+)s# and $thistime+=$1/3600;
			$ctime=$thistime;
		}
	}
	return $ctime;
}
# usage
sub usage {
	print STDERR "usage: $0 [-s] -d <datatype> <-r searchfilename | filename [filename] ...>
	where <datatype> is one of:
		E    - Total energy
		Ef   - Fermi energy
		S    - stress tensor
		F    - forces
		p    - phonon frequencies
		Trn  - Occupation matrix traces (for Hubbard U calculation)
		mabs - absolute magnetization
		mtot - total magnetization
		ctime - CPU time
	Options:
		-r searchfilename - run recursively, search for files name 'searchfilename'
		-s                - strip first character from dirnames when running recursively\n";
	exit -1;
}

# ------------------------------------------------------------------------------------
#   Main program
# ------------------------------------------------------------------------------------

our ($opt_d,$opt_r,$opt_s);
# coderef list of all extractors
my %extract = (
	E => \&extract_E,
	F => \&extract_F,
	S => \&extract_S,
	Ef => \&extract_Ef,
	p => \&extract_omega,
	Trn => \&extract_trn,
	mabs => \&extract_mabs,
	mtot => \&extract_mtot,
	ctime => \&extract_ctime
	);

getopts("d:r:s");
usage() unless ($opt_d);
my $extract=$extract{$opt_d};

my @filelist;

if ($opt_r) {
	opendir PWD,".";
	@filelist = grep { -f $_ } map { "$_/$opt_r" } readdir(PWD);
	closedir PWD;
} else {
	@filelist = @ARGV;
}
$column_seperator=" " if (@filelist>1);

my %result;
foreach my $filename (@filelist) {
	$filename =~ m#([^/]+)/[^/]+$#;
	my $dir = $1;
	$opt_s and $dir=~s/^.//;

	my $result_fh;
	open $result_fh,"$filename";
	my @RES = &$extract($result_fh);
	$result{$dir}=$RES[-1] if (@RES);
	close $result_fh;
}

foreach (sort { $a <=> $b} keys %result) {
	my $prefix='';
	$prefix = "$_$column_seperator" if (@filelist>1);
	print "$prefix$result{$_}\n" unless ($result{$_} eq '');
}
